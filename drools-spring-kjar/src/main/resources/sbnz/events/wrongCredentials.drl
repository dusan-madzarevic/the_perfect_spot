package sbnz.events;

import com.ftn.uns.ac.rs.theperfectmeal.cep.WrongCredentialsEvent;
import com.ftn.uns.ac.rs.theperfectmeal.model.User;
import com.ftn.uns.ac.rs.theperfectmeal.util.LoginAlarm;

declare SuspiciousUserEvent
    @role(event)
    @expires(24h)
    user: User
    reason: String
end;

global LoginAlarm loginAlarm;

rule "3 login attemps with wrong credentials during 1 minute by a single user"
	agenda-group "wrong-credentials"
	when
		$wce1: WrongCredentialsEvent($id: user.getId(), $user: user)
		Number(intValue == 3) from accumulate (
			$wce2: WrongCredentialsEvent(
				user.getId() == $id) over window:time(1m),
			count($wce2)
		)
		not (SuspiciousUserEvent(user.getId() == $id, 
        	reason == "3 login attemps with wrong credentials during 1 minute by a single user"))
	then
    	String reason = "3 login attemps with wrong credentials during 1 minute by a single user";
        insert(new SuspiciousUserEvent($user, reason));
        System.out.println("3 login attemps with wrong credentials during 1 minute by a single user " + $user.getEmail());
        loginAlarm.setUserId($id);
end;